//@version=4
study("Bollinger Bands + SSL channel + 50/200 EMA + Potato + SuperTrend", shorttitle="bb+ssl+ema+pot+st", overlay=true)

// bb
bbLen=input(title="BB Length", defval=20)
bbDev=input(title="BB Deviation", defval=2)
[middle, upper, lower] = bb(close, bbLen, bbDev)
plot(middle, "BB Basis", color=#872323)
bbp1 = plot(upper, "BB Upper", color=color.teal)
bbp2 = plot(lower, "BB Lower", color=color.teal)
fill(bbp1, bbp2, title = "BB Background", color=#198787, transp=95)

//ssl channel
period=input(title="SSL Period", defval=10)
len=input(title="SSL length", defval=10)
smaHigh=sma(high, len)
smaLow=sma(low, len)
float Hlv = na
Hlv := close > smaHigh ? 1 : close < smaLow ? -1 : Hlv[1]
sslDown = Hlv < 0 ? smaHigh: smaLow
sslUp   = Hlv < 0 ? smaLow : smaHigh

plot(sslDown, title="SSL Down", linewidth=1, color=color.red)
plot(sslUp, title="SSL UP", linewidth=1, color=color.lime)

// ema's
ema50=input(title="50 EMA", defval=50)
plot(ema(close,ema50), title="50 EMA" ,color = color.purple)
ema200=input(title="200 EMA", defval=200)
plot(ema(close,ema200), title="200 EMA" ,color = color.fuchsia)

// vwap
plot(vwap(hlc3), title="VWAP", color = color.blue)

// potato
potato_sources = input(defval=close, title="Potato Source")
potato_isHA = input(false, "Potato Use HA Candles", input.bool)
potato_heikenashi_1 = heikinashi(syminfo.tickerid)
potato_security_1 = security(potato_heikenashi_1, timeframe.period, potato_sources)
potato_src = potato_isHA ? potato_security_1 : potato_sources

potato_per = input(defval=27, minval=1, title="Potato Sampling Period")

// Range Multiplier
potato_mult = input(defval=1.0, minval=0.1, title="Potato Range Multiplier")

// Smooth Average Range
potato_smoothrng(x, t, m) =>
    wper = t * 2 - 1
    avrng = ema(abs(x - x[1]), t)
    smoothrng = ema(avrng, wper) * m
    smoothrng
potato_smrng = potato_smoothrng(potato_src, potato_per, potato_mult)

// Range Filter
potato_rngfilt(x, r) =>
    rngfilt = x
    rngfilt := x > nz(rngfilt[1]) ? x - r < nz(rngfilt[1]) ? nz(rngfilt[1]) : x - r : 
       x + r > nz(rngfilt[1]) ? nz(rngfilt[1]) : x + r
    rngfilt
potato_filt = potato_rngfilt(potato_src, potato_smrng)

// Filter Direction
potato_upward = 0.0
potato_upward := potato_filt > potato_filt[1] ? nz(potato_upward[1]) + 1 : potato_filt < potato_filt[1] ? 0 : nz(potato_upward[1])
potato_downward = 0.0
potato_downward := potato_filt < potato_filt[1] ? nz(potato_downward[1]) + 1 : potato_filt > potato_filt[1] ? 0 : nz(potato_downward[1])

// Target Bands
potato_hband = potato_filt + potato_smrng
potato_lband = potato_filt - potato_smrng

// Colors
potato_filtcolor = potato_upward > 0 ? color.lime : potato_downward > 0 ? color.red : color.orange
potato_barcolor = potato_src > potato_filt and potato_src > potato_src[1] and potato_upward > 0 ? color.lime :
   potato_src > potato_filt and potato_src < potato_src[1] and potato_upward > 0 ? color.green :
   potato_src < potato_filt and potato_src < potato_src[1] and potato_downward > 0 ? color.red :
   potato_src < potato_filt and potato_src > potato_src[1] and potato_downward > 0 ? color.maroon : color.orange

// Target
potato_hbandplot = plot(potato_hband, color=color.aqua, transp=100, title="Potato High Target")
potato_lbandplot = plot(potato_lband, color=color.fuchsia, transp=100, title="Potato Low Target")

// Break Outs
potato_longCond = bool(na)
potato_shortCond = bool(na)
potato_longCond := potato_src > potato_filt and potato_src > potato_src[1] and potato_upward > 0 or
   potato_src > potato_filt and potato_src < potato_src[1] and potato_upward > 0
potato_shortCond := potato_src < potato_filt and potato_src < potato_src[1] and potato_downward > 0 or
   potato_src < potato_filt and potato_src > potato_src[1] and potato_downward > 0

potato_CondIni = 0
potato_CondIni := potato_longCond ? 1 : potato_shortCond ? -1 : potato_CondIni[1]
potato_longCondition = potato_longCond and potato_CondIni[1] == -1
potato_shortCondition = potato_shortCond and potato_CondIni[1] == 1

//Alerts
plotshape(potato_longCondition, title="Potato Buy Signal", text="buy", textcolor=color.white, style=shape.triangleup, size=size.tiny, location=location.belowbar, color=color.green, transp=0)
plotshape(potato_shortCondition, title="Potato Sell Signal", text="sell", textcolor=color.white, style=shape.triangledown, size=size.tiny, location=location.abovebar, color=color.red, transp=0)

// Super Trend
st_Periods = input(title="SuperTrend ATR Period", type=input.integer, defval=10)
st_src = input(hl2, title="SuperTrend Source")
st_Multiplier = input(title="SuperTrend ATR Multiplier", type=input.float, step=0.1, defval=3.0)
st_changeATR= input(title="SuperTrend Change ATR Calculation Method ?", type=input.bool, defval=true)
st_showsignals = input(title="SuperTrend Show Buy/Sell Signals ?", type=input.bool, defval=true)
st_highlighting = input(title="SuperTrend Highlighter On/Off ?", type=input.bool, defval=true)
st_atr2 = sma(tr, st_Periods)
st_atr= st_changeATR ? atr(st_Periods) : st_atr2
st_up=st_src-(st_Multiplier*st_atr)
st_up1 = nz(st_up[1],st_up)
st_up := close[1] > st_up1 ? max(st_up,st_up1) : st_up
st_dn=st_src+(st_Multiplier*st_atr)
st_dn1 = nz(st_dn[1], st_dn)
st_dn := close[1] < st_dn1 ? min(st_dn, st_dn1) : st_dn
st_trend = 1
st_trend := nz(st_trend[1], st_trend)
st_trend := st_trend == -1 and close > st_dn1 ? 1 : st_trend == 1 and close < st_up1 ? -1 : st_trend
st_upPlot = plot(st_trend == 1 ? st_up : na, title="SuperTrend Up Trend", style=plot.style_linebr, linewidth=2, color=color.green)
st_buySignal = st_trend == 1 and st_trend[1] == -1
plotshape(st_buySignal ? st_up : na, title="SuperTrend UpTrend Begins", location=location.absolute, style=shape.circle, size=size.tiny, color=color.green, transp=0)
plotshape(st_buySignal and st_showsignals ? st_up : na, title="SuperTrend Buy", text="Buy", location=location.absolute, style=shape.labelup, size=size.tiny, color=color.green, textcolor=color.white, transp=0)
st_dnPlot = plot(st_trend == 1 ? na : st_dn, title="SuperTrend Down Trend", style=plot.style_linebr, linewidth=2, color=color.red)
st_sellSignal = st_trend == -1 and st_trend[1] == 1
plotshape(st_sellSignal ? st_dn : na, title="DownTrend Begins", location=location.absolute, style=shape.circle, size=size.tiny, color=color.red, transp=0)
plotshape(st_sellSignal and st_showsignals ? st_dn : na, title="SuperTrend Sell", text="Sell", location=location.absolute, style=shape.labeldown, size=size.tiny, color=color.red, textcolor=color.white, transp=0)
st_mPlot = plot(ohlc4, title="", style=plot.style_circles, linewidth=0)
st_longFillColor = st_highlighting ? (st_trend == 1 ? color.green : color.white) : color.white
st_shortFillColor = st_highlighting ? (st_trend == -1 ? color.red : color.white) : color.white
fill(st_mPlot, st_upPlot, title="SuperTrend UpTrend Highligter", color=st_longFillColor)
fill(st_mPlot, st_dnPlot, title="SuperTrend DownTrend Highligter", color=st_shortFillColor)
alertcondition(st_buySignal, title="SuperTrend Buy", message="SuperTrend Buy!")
alertcondition(st_sellSignal, title="SuperTrend Sell", message="SuperTrend Sell!")
st_changeCond = st_trend != st_trend[1]
alertcondition(st_changeCond, title="SuperTrend Direction Change", message="SuperTrend has changed direction!")
