//@version=4
strategy(title="Crossing MACD Strategy", shorttitle="CMACD Strat")

// MACD
// Getting inputs
fast_length = input(title="Fast Length", type=input.integer, defval=12)
slow_length = input(title="Slow Length", type=input.integer, defval=26)
src = input(title="Source", type=input.source, defval=close)
signal_length = input(title="Signal Smoothing", type=input.integer, minval = 1, maxval = 50, defval = 9)
sma_source = input(title="Simple MA(Oscillator)", type=input.bool, defval=false)
sma_signal = input(title="Simple MA(Signal Line)", type=input.bool, defval=false)
// Plot colors
col_grow_above = #26A69A
col_grow_below = #FFCDD2
col_fall_above = #B2DFDB
col_fall_below = #EF5350
col_macd = #0094ff
col_signal = #ff6a00
// Calculating
fast_ma = sma_source ? sma(src, fast_length) : ema(src, fast_length)
slow_ma = sma_source ? sma(src, slow_length) : ema(src, slow_length)
macd = fast_ma - slow_ma
signal = sma_signal ? sma(macd, signal_length) : ema(macd, signal_length)
hist = macd - signal
plot(hist, title="Histogram", style=plot.style_columns, color=(hist>=0 ? (hist[1] < hist ? col_grow_above : col_fall_above) : (hist[1] < hist ? col_grow_below : col_fall_below) ), transp=0 )
plot(macd, title="MACD", color=col_macd, transp=0)
plot(signal, title="Signal", color=col_signal, transp=0)

// Actual Strategy

ema_short_length = input(title="Short EMA", type=input.integer, defval=100)
ema_long_length = input(title="Long EMA", type=input.integer, defval=200)

ema_short = ema(close, ema_short_length)
ema_long = ema(close, ema_long_length)
combined_uptrend = input(title="Use combinded EMA uptrend", type=input.bool, defval=false)
sell_on_losing_momentum = input(title="Sell on losing momentum", type=input.bool, defval=false)

is_uptrend = if combined_uptrend
    close >= ema_short and ema_short >= ema_long
else
    close >= ema_long

signal_buy = crossover(macd, signal) and macd <= 0 and is_uptrend

default_signal_sell = crossunder(macd, signal) and macd > 0
signal_sell = if (sell_on_losing_momentum)
    signal > 0 and macd > 0 and hist[0] < hist[1]
else
    default_signal_sell


plot(signal_buy ? macd : na, title="Low MACD crossover", style=plot.style_cross, color=#0094FF, linewidth=3)
plot(signal_sell ? macd : na, title="High MACD crossunder", style=plot.style_cross, color=#FF6A00, linewidth=3)

if (signal_buy)
    strategy.entry("MACD Long", strategy.long)

if (signal_sell)
    strategy.close("MACD Long", comment="MACD close")
